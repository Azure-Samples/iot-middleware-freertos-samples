# Copyright (c) Microsoft Corporation. All rights reserved.
# SPDX-License-Identifier: MIT

cmake_minimum_required(VERSION 3.19)

set(ROOT_PATH
    ${CMAKE_CURRENT_LIST_DIR}/../../../../../..
)

set(AZURE_IOT_MIDDLEWARE_FREERTOS
    ${ROOT_PATH}/libs/azure-iot-middleware-freertos
)

file(GLOB_RECURSE COMPONENT_SOURCES
    ${AZURE_IOT_MIDDLEWARE_FREERTOS}/source/*.c
)

list(APPEND COMPONENT_SOURCES
    ${AZURE_IOT_MIDDLEWARE_FREERTOS}/ports/coreMQTT/azure_iot_core_mqtt.c
)

idf_component_get_property(FREERTOS_DIR freertos COMPONENT_DIR)
idf_component_get_property(FREERTOS_RELATIVE_INCLUDE_DIRS freertos INCLUDE_DIRS)

foreach(FREERTOS_RELATIVE_INCLUDE_DIR ${FREERTOS_RELATIVE_INCLUDE_DIRS})
    file(REAL_PATH ${FREERTOS_RELATIVE_INCLUDE_DIR} FREERTOS_ABSOLUTE_INCLUDE_DIR BASE_DIRECTORY ${FREERTOS_DIR})
    list(APPEND FREERTOS_ABSOLUTE_INCLUDE_DIRS ${FREERTOS_ABSOLUTE_INCLUDE_DIR})

    set(FREERTOS_ABSOLUTE_INCLUDE_FREERTOS_DIR "${FREERTOS_ABSOLUTE_INCLUDE_DIR}/freertos")
    if(EXISTS "${FREERTOS_ABSOLUTE_INCLUDE_FREERTOS_DIR}")
        list(APPEND FREERTOS_ABSOLUTE_INCLUDE_DIRS ${FREERTOS_ABSOLUTE_INCLUDE_FREERTOS_DIR})
    endif()
endforeach()

set(COMPONENT_INCLUDE_DIRS
    ${FREERTOS_ABSOLUTE_INCLUDE_DIRS}
    ${AZURE_IOT_MIDDLEWARE_FREERTOS}/source/include
    ${AZURE_IOT_MIDDLEWARE_FREERTOS}/source/interface
    ${AZURE_IOT_MIDDLEWARE_FREERTOS}/ports/coreMQTT
)

idf_component_register(
    SRCS ${COMPONENT_SOURCES}
    INCLUDE_DIRS ${COMPONENT_INCLUDE_DIRS}
    REQUIRES freertos azure-sdk-for-c coreMQTT)
